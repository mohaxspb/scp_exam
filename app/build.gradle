apply plugin: 'com.android.application'

//kotlin
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
//new crash reporting
apply plugin: 'io.fabric'

//here is script to generate version code from version name
//which is set in gradle.properties as value of version var
//which must be in format major, minor, patch, where patch goes from 0 to 99
apply from: '../versioning.gradle'

android {
    namespace "com.scp.scpexam"
    compileSdkVersion 34

    defaultConfig {
        applicationId "com.scp.scpexam"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode buildVersionCode()
        versionName version
//        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [toothpick_registry_package_name: 'com.scp.scpexam']
            }
        }

//        kapt {
//            arguments {
//                arg("room.schemaLocation", "$projectDir/schemas".toString())
//            }
//        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfigs {
                release {
                    // Load keystore
                    if (rootProject.file('keystore.properties').exists()) {
                        def keystorePropertiesFile = rootProject.file("keystore.properties")
                        def keystoreProperties = new Properties()
                        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                        storeFile file(keystoreProperties['storeFile'])
                        storePassword keystoreProperties['storePassword']
                        keyAlias keystoreProperties['keyAlias']
                        keyPassword keystoreProperties['keyPassword']
                    }
                }
            }

            if (rootProject.file('keystore.properties').exists()) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            debuggable true

            //test adUnit IDs from https://developers.google.com/admob/android/test-ads#enable_test_devices
            resValue('string', 'ad_unit_id_interstitial', '24534e1901884e398f1253216226017e')
            resValue('string', 'ad_unit_id_banner', 'b195f8dd8ded45fe847ad89ed1d016da')
            resValue('string', 'ad_unit_id_rewarded_video', '920b6145fb1546cf8b5cf2ac34638bb7')
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }
    
    flavorDimensions "mode"

    productFlavors {
        prod {
            manifestPlaceholders = [myDebuggable: false]
            buildConfigField "boolean", "TIMBER_ENABLE", "false"
            dimension "mode"
        }
        dev {
            dimension "mode"
            manifestPlaceholders = [myDebuggable: true]
            aaptOptions.cruncherEnabled = false
            resConfigs("ru", "xxhdpi")
        }
        localUra { dimension "mode" }
        localHome { dimension "mode" }

        applicationVariants.all { variant ->
            println("variantCore: " + variant.productFlavors[0].name)

            if (rootProject.file("config/" + variant.productFlavors[0].name + "/config.properties").exists()) {
                def properties = new Properties()
                properties.load(new FileInputStream(rootProject.file("config/" + variant.productFlavors[0].name + "/config.properties")))

                //analitics
                buildConfigField('String', 'YANDEX_METRICA_API_KEY', '"' + properties.get("yandexMetricaApiKey") + '"')
                //analitics END

                //ads
                //mopub
                resValue('string', 'ad_unit_id_banner', properties.get("mopubBanner"))
                resValue('string', 'ad_unit_id_interstitial', properties.get("mopubInterstitial"))
                resValue('string', 'ad_unit_id_rewarded_video', properties.get("mopubRewardedVideo"))

                //quiz api
                buildConfigField('String', 'QUIZ_API_URL', '"' + properties.get("quizApiUrl") + '"')
                //quiz api auth
                buildConfigField('String', 'USER', '"' + properties.get("user") + '"')
                buildConfigField('String', 'PASSWORD', '"' + properties.get("password") + '"')
                //google server_client_id
                buildConfigField('String', 'SERVER_GOOGLE_CLIENT_ID', '"' + properties.get("serverGoogleClientId") + '"')

                buildConfigField('String', 'CLIENT_ID', '"' + properties.get("clientId") + '"')
                buildConfigField('String', 'CLIENT_SECRET', '"' + properties.get("clientSecret") + '"')
                //quiz api ssl pinning
                buildConfigField('String', 'SSL_PINNING_DOMAIN', '"' + properties.get("sslPinningDomain") + '"')
                buildConfigField('String', 'SSL_PINNING_VALUE', '"' + properties.get("sslPinningValue") + '"')

            } else {
                throw new GradleException(
                        "You need to have projectRoot/config/config.properties file with "
                                + "yandexMetricaApiKey, "
                                + "mopubInterstitial, "
                                + "mopubBanner, "
                                + "to build project"
                )
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    //support
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.recyclerview:recyclerview:1.3.0"
    implementation "com.google.android.material:material:1.9.0"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.core:core-ktx:1.10.1"
    implementation "androidx.activity:activity-ktx:1.7.2"

    //multidex to support more than 65K methods
//    implementation 'androidx.multidex:multidex:2.0.1'

    //google libs
    implementation 'com.google.firebase:firebase-core:21.1.1'
//    implementation 'com.google.firebase:firebase-analytics:21.3.0'
    implementation 'com.google.firebase:firebase-messaging:23.2.0'
    implementation 'com.google.android.gms:play-services-base:18.2.0'

    // Import the BoM for the Firebase platform
    implementation(platform("com.google.firebase:firebase-bom:32.2.0"))

    // Add the dependencies for the Crashlytics and Analytics libraries
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation("com.google.firebase:firebase-crashlytics-ktx")
    implementation("com.google.firebase:firebase-analytics-ktx")

    // AppMetrica
    implementation 'com.yandex.android:mobmetricalib:5.3.0'

    // Worker
    implementation 'androidx.work:work-runtime-ktx:2.8.1'
    implementation 'androidx.work:work-rxjava2:2.8.1'

    //DI
    String toothPickVersion = '3.1.0'
    implementation "com.github.stephanenicolas.toothpick:toothpick-runtime:$toothPickVersion"
    implementation "com.github.stephanenicolas.toothpick:smoothie:$toothPickVersion"
    kapt "com.github.stephanenicolas.toothpick:toothpick-compiler:$toothPickVersion"

    //mvp
    //moxy
    String moxyVersion = '2.0.2'
    kapt "com.github.moxy-community:moxy-compiler:$moxyVersion"
    implementation "com.github.moxy-community:moxy-androidx:$moxyVersion"
    implementation "com.github.moxy-community:moxy-ktx:$moxyVersion"

    //routing
    implementation 'ru.terrakok.cicerone:cicerone:5.1.0'

    //rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.18'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.1'

    //db
    def roomVersion = '2.6.0'
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // Retrofit
    def okHttpVersion = '4.11.0'
    def retrofitVersion = '2.9.0'
    implementation("com.squareup.retrofit2:retrofit:$retrofitVersion") { exclude module: 'okhttp' }
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    //json
    def moshiVersion = "1.15.0"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "com.squareup.moshi:moshi-adapters:$moshiVersion"
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    //adapter delegate
    implementation 'com.hannesdorfmann:adapterdelegates3:3.1.0'

    //logs
    implementation 'com.jakewharton.timber:timber:5.0.1'

    //image
    implementation 'com.github.bumptech.glide:glide:4.15.1'
    kapt 'com.github.bumptech.glide:compiler:4.10.0'

    //dialog
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'

    //views
    implementation 'com.google.android:flexbox:1.0.0'
    implementation 'jp.wasabeef:blurry:2.1.1'

    //inapp
    implementation 'com.android.billingclient:billing:6.0.1'

    //rate app
//    implementation('com.scp.scpexam.utils:rate:1.2.1@aar') { transitive = true }
    implementation('ru.kuchanov.utils:rate:1.2.1@aar') { transitive = true }
    //social auth
    //google
    implementation 'com.google.android.gms:play-services-auth:20.6.0'
    //facebook
    implementation 'com.facebook.android:facebook-login:5.1.1'
    //vk
    implementation 'com.vk:android-sdk-core:4.0.1'
    implementation 'com.vk:android-sdk-api:4.0.1'

    //testing
    testImplementation 'junit:junit:4.12'
    testImplementation('androidx.test.ext:junit:1.1.1')
    testImplementation('androidx.test:rules:1.2.0')

    //flags
    implementation 'com.github.KaKaVip:Android-Flag-Kit:v0.1'

    //mopub
    implementation('com.mopub:mopub-sdk:+@aar') { transitive = true }
}
//google
apply plugin: 'com.google.gms.google-services'